// my dumbo drop system by ben age 8
-	script	DropSystem	-1,{
  OnNPCKillEvent: // 25% Base item drop chance
		callfunc("HeyAnItemDrop",25,getmonsterinfo(killedrid,1),1);

		if(rand(1,10000) <= 300) // Mythril 3%
			getitem3 57000,1,1,0,0,0,0,0,0;
		if(rand(1,10000) <= 300) // Orichalcum 3%
			getitem3 57001,1,1,0,0,0,0,0,0;
		if(rand(1,10000) <= 25)  // Divine Hammer .25%
			getitem3 57008,1,1,0,0,0,0,0,0;
		
		if(rand(1,10000) <= 200) // Empty 2%
			getitem3 57002,1,1,0,0,0,0,0,0;			
		if(rand(1,10000) <= 100) // Arcane 1%
			getitem3 57004,1,1,0,0,0,0,0,0;			
		if(rand(1,10000) <= 25)  // Blessed 0.25%
			getitem3 57005,1,1,0,0,0,0,0,0;
		if(rand(1,10000) <= 20)  // Capricious 0.2%
			getitem3 57006,1,1,0,0,0,0,0,0;
		if(rand(1,10000) <= 10)  // Discordant 0.1%
			getitem3 57007,1,1,0,0,0,0,0,0;
}

function	script	HeyAnItemDrop	{
    set .@drop,getarg(0);
    set .@monlvl,getarg(1);
    set .@random,rand(1,10000);
    set .@item,0;
    set .@socket1,0;
    set .@socket2,0;
    set .@socket3,0;
    set .@socket4,0;
    set .@ilvl,0;
    set .@quality,0;
    set .@itemfind,0;
    set .@magicfind,0;
    set .@rolls,0;
    
    set .@itemfind,100 + readparam(bLuk2);
    set .@magicfind,readparam(bLuk2);
    
    // Do we get a drop at all?
    if((.@drop * .@itemfind) > .@random) {
      set .@random,rand(1,10000);
      
      if(.@random >= 9750-(.@magicfind * 5 / 2)) { // Check for unique - 2.5% base
        switch(rand(0,2)) { // 0 - Armor, 1 - Accessory, 2 - Weapon
          case 0: set .@item,52000+rand(0,34); break;
          case 1: set .@item,53000+rand(0,9); break;
          case 2: set .@item,54000+rand(0,34); break;
        }
      } else { // Common Item
        switch(rand(0,1)) { // 0 - Armor, 1 - Weapon
          case 0: set .@item,50000+rand(0,17); break;
          case 1: set .@item,51000+rand(0,11); break;
        }
      }
      
      if(.@item < 53000 || .@item > 53999) { // FIX LATER!!!!  I hate it. Accessory check. They don't get quality/ilvl
        set .@ilvl,.@monlvl;
        set .@quality,rand(50,125);
      }
      
      // Random Bonuses
      // Base
      //  5%    3 + Seal
      //  10%   3
      //  15%   2
      //  30%   1
      //  40%   0
      
      set .@random,rand(1,10000);
      
      if(.@random >= 9500-(.@magicfind * 5)) { // there's probably a less shitty way to do this but w/e lol
        set .@socket1,rand(55000,55018);
        set .@socket2,rand(55000,55018);
        set .@socket3,rand(55000,55018);
        set .@socket4,rand(55100,55118);
      } else if (.@random >= 8500-(.@magicfind * 10)) {
        set .@socket1,rand(55000,55018);
        set .@socket2,rand(55000,55018);
        set .@socket3,rand(55000,55018);      
      } else if (.@random >= 7000-(.@magicfind * 15)) {
        set .@socket1,rand(55000,55018);
        set .@socket2,rand(55000,55018);     
      } else if(.@random >= 4000-(.@magicfind * 30)) {
        set .@socket1,rand(55000,55018);
      }
      
      set .@rolls,rand(0,99);
      set .@rolls,.@rolls + (rand(0,99) << 8);
      set .@rolls,.@rolls + (rand(0,99) << 16);
      set .@rolls,.@rolls + (rand(0,99) << 24);
       
       
      // Finally, generate the item!
      // Flags
      // 0 - Give to player
      // 1 - Drop on the ground (monster kills, etc)
      if(getarg(2))
				getitem5 .@item,1,1,.@quality,.@ilvl,.@socket1,.@socket2,.@socket3,.@socket4,.@rolls;
			else
				getitem2 .@item,1,1,.@quality,.@ilvl,.@socket1,.@socket2,.@socket3,.@socket4;
    }
    
    return 1;
}