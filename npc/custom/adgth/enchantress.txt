// Enchantress Script v1.0

valkyrie,45,52,6	script	Undina#adgth	504,{
	disable_items;
	mes "[Undina]";
	mes "Welcome! What do ya need?";
	menu "I need some enchanting done!",menuEnchant,"What are your services?",askServices,"Nothing today.",thatsAll;
	cleartext;
	
backToMenu:

	cleartext;
	mes "[Undina]";
	mes "So, what's up?";
	menu "I need some enchanting done!",menuEnchant,"What are your services?",askServices,"That's all.",thatsAll;
	
askServices:

	cleartext;
	mes "[Undina]";
	mes "Well, I'm an enchantress... so I can, you know, enchant items. As long as you got the right stuff for me!";
	next;
	mes "[Undina]";
	mes "Enchanting revolves around Bijous, magic-laced jewels that can be found on monsters. They go crazy over them! But then, so do we enchantresses!";
	next;
	mes "[Undina]";
	mes "Empty Bijous may only seem like shiny rocks, but they got the potential to hold a ton of magic! Using one, I can store a Shard from an enchanted item to create a Fervent Bijou!";
	next;
	mes "[Undina]";
	mes "I can use the magic within a Fervent Bijou to replace a Shard in whatever you want. These Fervent Bijous can be used to totally customize your equipment! A couple warnings, though...";
	next;
	mes "[Undina]";
	mes "I'm pretty good at what I do, but I can't store Seals within an Empty Bijou. I also can't add additional enchantments to an item with only a Fervent Bijou, only replace existing ones.";
	mes " ";
	mes "That stuff is way beyond my powers!";
	next;
	mes "[Undina]";
	mes "And be careful, the item used to create a Fervent Bijou won't be left in... usable condition. So, definitely don't bring me anything you're attached to!";
	next;
	mes "[Undina]";
	mes "Beyond that, there are also rare Bijous out there already charged with magic! They can do all sorts of stuff, so bring me one and I'll see what I can do, yeah?";
	next;
	goto backToMenu;
	
menuEnchant:

	cleartext;
	mes "[Undina]";
	mes "Oh yeah? What kind?";
	menu "Enscribe Empty Bijou.",enchEmpty,"Enchant Item.",enchBijou,"I've changed my mind.",thatsAll;
	cleartext;

enchEmpty:

	cleartext;
	mes "[Undina]";
	mes "Let's get to it, then! Remember, taking a Shard from an item will ^FF0000irreversably destroy it^000000! Like, totally wreck it! Be sure it's nothing you're gonna miss, yeah?";
	next;
	mes "[Undina]";
	mes "What're we working with?";
	setarray .@position$[1],"Body","Left Hand","Right Hand","Gloves","Belt","Robe","Shoes","Accessory 1", "Accessory 2";
	set .@menu$,"";
	for(set .@i,1; .@i<=9; set .@i,.@i+1) {
		if (getequipisequiped(.@i+1)) {
			set .@menu$, .@menu$ + getequipname(.@i+1);
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	
	if (.@equipped == 0) {
		mes "[Undina]";
		mes "Uh... you actually need some equipment if we're gonna do this...";
		close;
	}
	set .@part, select(.@menu$)+1;

	if (!getequipisequiped(.@part)) { //custom check
		cleartext;	
		mes "[Undina]";
		mes "Uh... you actually need some equipment if we're gonna do this...";
		close;
	}
	
	if (getequipcardcnt(.@part) == 0) {
		cleartext;	
		mes "[Undina]";
		mes "I can't make something from nothing, pal. Bring me something enchanted!";
		close;
	}
		
	if (countitem(57002) == 0) {
		cleartext;	
		mes "[Undina]";
		mes "You know you need an Empty Bijou for this, right?";
		close;
	}
	
	if (getequipcardcnt(.@part) == 4) {
		cleartext;	
		mes "[Undina]";
		mes "Are you sure you want to use this item...? It's overflowing with magic energy!";
		menu "On second thought...",-,"Yeah, I'm sure.",enchEmpty2;
		cleartext;	
		mes "[Undina]";
		mes "Phew, okay. I'd hate to have to destroy something like that!";
		close;
	}
	
	if (getequipid(.@part) > 51999) {
		cleartext;	
		mes "[Undina]";
		mes "Are you sure you want to use this item...? It looks pretty rare!";
		menu "On second thought...",-,"Yeah, I'm sure.",enchEmpty2;
		cleartext;	
		mes "[Undina]";
		mes "Phew, okay. I'd hate to have to destroy something like that!";
		close;
	}

enchEmpty2:

	cleartext;
	mes "[Undina]";
	mes "Nice! So which of these am I ripping out?";
	set .@menu2$,"";
	for(set .@i,0; .@i<=2; set .@i,.@i+1)
		set .@menu2$, .@menu2$+((getequipcardid(.@part,.@i))?getitemname((getequipcardid(.@part,.@i))):"")+ ":";
	set .@slot, select(.@menu2$) - 1;
	set .@ench, getequipcardid(.@part,.@slot);
	
	switch (.@slot) {
		case 0: set .@roll,getequiprolls(.@part) & 0x000000FF; break;
		case 1: set .@roll,(getequiprolls(.@part) & 0x0000FF00) >> 8; break;
		case 2: set .@roll,(getequiprolls(.@part) & 0x00FF0000) >> 16; break;
	}

	delitem 57002,1;
	delequip .@part;
	getitem4 57003,1,1,0,0,.@ench,0,0,0,.@roll;
	
	specialeffect2 610;
	
	cleartext;
	mes "[Undina]";
	mes "Tada! Simple as that! Sorry about your stuff, but uh... I'm sure Vorser will appreciate the scrap metal!";
	close;
	
enchBijou:

	cleartext;
	mes "[Undina]";
	mes "What're we working with?";
	getinventorylist;
	set .@menu$,"";
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1) {
		if (@inventorylist_id[.@i] == 57003 ||
				@inventorylist_id[.@i] == 57004 ||
				@inventorylist_id[.@i] == 57005 ||
				@inventorylist_id[.@i] == 57006 ||
				@inventorylist_id[.@i] == 57007) {
			set .@menu$, .@menu$+(@inventorylist_id[.@i] == 57003 ? getitemname(@inventorylist_id[.@i])+" ["+getitemname(@inventorylist_card1[.@i])+"]":getitemname(@inventorylist_id[.@i]));
		}
		set .@menu$, .@menu$ + ":";
	}
	
	set .@selection, select(.@menu$);
	set .@bijou,@inventorylist_id[.@selection-1];
	set .@bijouroll,@inventorylist_rolls[.@selection-1];
	set .@shard,@inventorylist_card1[.@selection-1];
	
	cleartext;
	mes "[Undina]";
	mes "Nice, nice. Which item are we working on?";
	setarray .@position$[1],"Body","Left Hand","Right Hand","Gloves","Belt","Robe","Shoes","Accessory 1", "Accessory 2";
	set .@menu2$,"";
	for(set .@i,1; .@i<=9; set .@i,.@i+1) {
		if (getequipisequiped(.@i+1)) {
			set .@menu2$, .@menu2$ + getequipname(.@i+1);
			set .@equipped,1;
		}
		set .@menu2$, .@menu2$ + ":";
	}
	
	if (.@equipped == 0) {
		cleartext;	
		mes "[Undina]";
		mes "Uh... you actually need some equipment if we're gonna do this...";
		close;
	}
	set .@part, select(.@menu2$)+1;

	
	switch(.@bijou) {
		case 57004: goto enchArcane; break;
		case 57005: goto enchBlessed; break;
		case 57006: goto enchCapricious; break;
		case 57007: goto enchDiscordant; break;
	}
	
enchFervent:

	cleartext;
	if (getequipcardcnt(.@part) == 0) {
		mes "[Undina]";
		mes "Hey! I can only replace enchantments with these, not add them! Give me something already enchanted to work on!";
		close;
	}

	mes "[Undina]";
	mes "Which one of these do you want to replace?";
	set .@menu3$,"";
	for(set .@i,0; .@i<=2; set .@i,.@i+1)
		set .@menu3$, .@menu3$+((getequipcardid(.@part,.@i))?getitemname((getequipcardid(.@part,.@i))):"")+ ":";
	set .@selection,select(.@menu3$) - 1;
	set .@ench, getequipcardid(.@part,.@selection);
	
	set .@type,getequipid(.@part);
	set .@qual,getequiprefinerycnt(.@part);
	set .@ilvl,getequipilvlcnt(.@part);
	set .@ench1,getequipcardid(.@part,0);
	set .@ench2,getequipcardid(.@part,1);
	set .@ench3,getequipcardid(.@part,2);
	set .@ench4,getequipcardid(.@part,3);
	
	set .@partroll,getequiprolls(.@part);
	set .@newroll,0;
	
	unequip .@part;
	delitem2 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4;	
	delitem2 .@bijou,1,1,0,0,.@shard,0,0,0;
	
	switch(.@selection) {
		case 0: 
			set .@ench1,.@shard;
			set .@partroll,.@partroll & ~(0xFF);
			set .@newroll,.@partroll | .@bijouroll;
			break;
		case 1: 
			set .@ench2,.@shard; 
			set .@partroll,.@partroll & ~(0xFF00);
			set .@bijouroll,.@bijouroll << 8;
			set .@newroll,.@partroll | .@bijouroll;
			break;
		case 2: 
			set .@ench3,.@shard; 
			set .@partroll,.@partroll & ~(0xFF0000);
			set .@bijouroll,.@bijouroll << 16;
			set .@newroll,.@partroll | .@bijouroll;
			break;
	}
	
	
	autoequip .@type,1;
	getitem4 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4,.@newroll;
	autoequip .@type,0;

	cleartext;
	mes "[Undina]";
	mes "Tada! Simple as that! Enjoy your stuff!";
	close;
	
enchArcane:

	cleartext;
	if (getequipcardcnt(.@part) >= 3) {
		mes "[Undina]";
		mes "Ah, this item already has the maximum number of Shards in it, I can't add any more with one of these. Bring me something less... enchanted, yeah?";
		close;
	}
	
	set .@type,getequipid(.@part);
	set .@qual,getequiprefinerycnt(.@part);
	set .@ilvl,getequipilvlcnt(.@part);
	set .@ench1,getequipcardid(.@part,0);
	set .@ench2,getequipcardid(.@part,1);
	set .@ench3,getequipcardid(.@part,2);
	set .@ench4,getequipcardid(.@part,3);
	set .@partroll,getequiprolls(.@part);
	set .@newroll,0;
	set .@rand99,rand(0,99);

	set .@numEnch,getequipcardcnt(.@part);
	unequip .@part;
	delitem2 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4;	
	delitem .@bijou,1;
	
	switch(.@numEnch) {
		case 0: 
			set .@ench1,rand(55000,55018);
			set .@partroll,.@partroll & ~(0xFF);
			set .@newroll,.@partroll | .@rand99;
			break;
		case 1: 
			set .@ench2,rand(55000,55018); 
			set .@partroll,.@partroll & ~(0xFF00);
			set .@newroll,.@partroll | (.@rand99 << 8);
			break;
		case 2: 
			set .@ench3,rand(55000,55018); 
			set .@partroll,.@partroll & ~(0xFF0000);
			set .@newroll,.@partroll | (.@rand99 << 16);
			break;
	}
	
	autoequip .@type,1;
	getitem4 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4,.@newroll;
	autoequip .@type,0;

	cleartext;	
	mes "[Undina]";
	mes "Tada! Simple as that! Enjoy your stuff!";
	close;
	
enchBlessed:

	cleartext;
	if (getequipcardcnt(.@part) < 3) {
		mes "[Undina]";
		mes "This item can't quite handle a Seal yet... I can only use this on items with at least three Shards.";
		close;
	}
	
	set .@type,getequipid(.@part);
	set .@qual,getequiprefinerycnt(.@part);
	set .@ilvl,getequipilvlcnt(.@part);
	set .@ench1,getequipcardid(.@part,0);
	set .@ench2,getequipcardid(.@part,1);
	set .@ench3,getequipcardid(.@part,2);
	set .@ench4,getequipcardid(.@part,3);
	set .@partroll,getequiprolls(.@part);
	set .@newroll,0;
	set .@rand99,rand(0,99);

	unequip .@part;
	delitem2 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4;	
	delitem .@bijou,1;

	set .@ench4,rand(55100,55112);
	set .@partroll, .@partroll & 0xFFFFFF;
	set .@newroll, .@partroll | (.@rand99 << 24);

	autoequip .@type,1;
	getitem4 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4,.@newroll;
	autoequip .@type,0;

	cleartext;	
	mes "[Undina]";
	mes "Tada! Simple as that! Enjoy your stuff!";
	close;
	
enchCapricious:

	cleartext;
	if (getequipcardcnt(.@part) == 0) {
		mes "[Undina]";
		mes "This Bijou can only work on items that are already enchanted. Add some Shards, and then we can talk, yeah?";
		close;
	}
	
	mes "[Undina]";		
	mes "Now, this Capricious Bijou will ^FF0000completely randomize^000000 the enchantments on here. You sure you're okay with that?";
	next;
	if(select("Sounds good!:I changed my mind.") == 2) {
		mes "[Undina]";
		mes "Ah, alright then.";
		close;
	}	
	
	set .@type,getequipid(.@part);
	set .@qual,getequiprefinerycnt(.@part);
	set .@ilvl,getequipilvlcnt(.@part);
	set .@ench1,getequipcardid(.@part,0);
	set .@ench2,getequipcardid(.@part,1);
	set .@ench3,getequipcardid(.@part,2);
	set .@ench4,getequipcardid(.@part,3);
	set .@newroll,0;

	unequip .@part;
	delitem2 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4;	
	delitem .@bijou,1;

	set .@ench1,.@ench1 ? rand(55000,55018) : 0;
	set .@ench2,.@ench2 ? rand(55000,55018) : 0;
	set .@ench3,.@ench3 ? rand(55000,55018) : 0;
	set .@ench4,.@ench4 ? rand(55100,55112) : 0;
	set .@newroll,.@newroll | rand(0,99);
	set .@newroll,.@newroll | (rand(0,99) << 8);
	set .@newroll,.@newroll | (rand(0,99) << 16);
	set .@newroll,.@newroll | (rand(0,99) << 24);

	autoequip .@type,1;
	getitem4 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4,.@newroll;
	autoequip .@type,0;

	cleartext;
	mes "[Undina]";
	mes "Tada! Simple as that! Enjoy your stuff!";
	close;
	
enchDiscordant:

	cleartext;
	mes "[Undina]";		
	mes "Now, this Discordant Bijou will ^FF0000completely warp^000000 this item... You sure you're okay with that?";
	next;
	if(select("Sounds good!:I changed my mind.") == 2) {
		mes "[Undina]";
		mes "Ah, alright then.";
		close;
	}	
	
	set .@type,getequipid(.@part);
	set .@qual,getequiprefinerycnt(.@part);
	set .@ilvl,getequipilvlcnt(.@part);
	set .@ench1,getequipcardid(.@part,0);
	set .@ench2,getequipcardid(.@part,1);
	set .@ench3,getequipcardid(.@part,2);
	set .@ench4,getequipcardid(.@part,3);
	set .@newroll,0;

	unequip .@part;
	delitem2 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4;	
	delitem .@bijou,1;

	if(rand(0,1))
		set .@qual,rand(60,100);
	else
		set .@qual,rand(90,135);

	if(rand(0,1))
		set .@ilvl,rand(BaseLevel)+1;
	else {
		set .@rnd,BaseLevel + 5 - rand(20);
		set .@ilvl,(.@rnd < 1 ? 1 : .@rnd);
	}

	if(rand(0,99) > 75) {
		set .@ench1,rand(55000,55018);
		set .@ench2,rand(55000,55018);
		set .@ench3,rand(55000,55018);
		set .@ench4,rand(55100,55112);
	} else if(rand(0,99) > 50) {
		set .@ench1,rand(55000,55018);
		set .@ench2,rand(55000,55018);
		set .@ench3,rand(55000,55018);
		set .@ench4,0;
	} else if(rand(0,99) > 25) {
		set .@ench1,rand(55000,55018);
		set .@ench2,rand(55000,55018);
		set .@ench3,0;
		set .@ench4,0;
	} else {
		set .@ench1,rand(55000,55018);
		set .@ench2,0;
		set .@ench3,0;
		set .@ench4,0;
	}
	
	set .@newroll,.@newroll | rand(0,99);
	set .@newroll,.@newroll | (rand(0,99) << 8);
	set .@newroll,.@newroll | (rand(0,99) << 16);
	set .@newroll,.@newroll | (rand(0,99) << 24);

	autoequip .@type,1;
	getitem4 .@type,1,1,.@qual,.@ilvl,.@ench1,.@ench2,.@ench3,.@ench4,.@newroll;
	autoequip .@type,0;

	cleartext;
	mes "[Undina]";
	mes "Tada! Simple as that! Enjoy your stuff!";
	close;

thatsAll:

	cleartext;
	mes "[Undine]";
	mes "I'll see you around!";
	close;
}